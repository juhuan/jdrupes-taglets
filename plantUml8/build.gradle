/*
 * JDrupes PlantUML Taglet
 * Copyright (C) 2017  Michael N. Lipp
 * 
 * This program is free software; you can redistribute it and/or modify it 
 * under the terms of the GNU General Public License as published by 
 * the Free Software Foundation; either version 3 of the License, or 
 * (at your option) any later version.
 * 
 * This program is distributed in the hope that it will be useful, but 
 * WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
 * or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License 
 * for more details.
 * 
 * You should have received a copy of the GNU General Public License along 
 * with this program; if not, see <http://www.gnu.org/licenses/>.
 */

plugins {
	id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = '1.8'

version = '0.1.0'

repositories {
	maven { url 'https://jitpack.io' }
}

configurations {
	mdoclet
}

dependencies {
	compile group:'net.sourceforge.plantuml', name:'plantuml', version:'8032'
    compileOnly libs.jdkTools

	// For easy debugging with Eclipse, add to "compile" as well and refresh gradle project	
	// mdoclet 'org.jdrupes:mdoclet-doclet:0.1.0'
	mdoclet 'com.github.mnlipp.jdrupes-mdoclet:mdoclet-doclet:0.1.0'
}

// Configure sensible layout
sourceSets {
    main {
        java {
            srcDir 'src'
        }
		resources {
			srcDir 'resources'
		}
    }
}

javadoc {
	dependsOn classes
	
	title = "PlantUML Taglet"

	options.docletpath = configurations.mdoclet.files.asType(List)
	options.doclet = "org.jdrupes.mdoclet.MDoclet"	
	options.tagletPath = (sourceSets.main.output + sourceSets.main.compileClasspath) as List
	options.taglets = ["org.jdrupes.taglets.plantUml.Taglet"]

	destinationDir = file("../../jdrupes-taglets.gh-pages/javadoc/")
	
	options.addStringOption("overview", "overview.md")
	options.addBooleanOption("disable-auto-highlight", true)
	options.addBooleanOption("linksource", true)
	options.links("http://docs.oracle.com/javase/8/docs/api/")
	// options.stylesheetFile(file("stylesheet.css"))
	options.addStringOption("bottom", file("../misc/javadoc.bottom.txt").text)
}

publishing {
	
	publications {
		mavenJava(MavenPublication) {
			artifactId = "plantuml-taglet"
			from components.java
		}
	}
}

bintray {
	user = project.properties['bintrayUser'] ?: "nouser"
	key = project.properties['bintrayApiKey'] ?: "nokey"
	publications = ['mavenJava']
	publish = true
	pkg {
		repo = project.group
		name = "plantuml-taglet"
		version {
			 name = project.version
		}
	}
 }
